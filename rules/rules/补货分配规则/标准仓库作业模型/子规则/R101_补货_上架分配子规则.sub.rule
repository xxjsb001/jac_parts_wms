
$可上架库位列表 = 创建列表()

如果(拣货分配返回列表 == 空 || 列表大小(拣货分配返回列表) <= 0) {
	异常("----在拣货区: " + 拣货区 + "中, 货品代码：" + 货品代码 + ", 无可用库存!")
}

列表对象排序(拣货分配返回列表, "批次序号","升序")

入锁库位集 = 多值查表("R101_基础数据_库位出入锁表", "入锁")
入锁拼接条件 = ""
入锁拼接条件 = 出入锁拼接条件(入锁库位集, 入锁拼接条件).出入锁拼接条件返回值

补货区库位定位分类列表 = 多值查表("R101_基础数据_库位定位分类表", 补货区特性.库位定位分类)

每个($库位定位分类 : 补货区库位定位分类列表) {
	补货区库位定位分类 = $库位定位分类
		
	补货区拼接条件=""
	补货区拼接条件 = 补货区拼接条件(补货区库位定位分类 , 补货区拼接条件).补货区拼接条件返回值
	

	$可上架库位集 = 数据查询("R101_补货_上架分配库位数据源", "仓库序号",仓库序号, "库区", $库位定位分类.库区, "入锁拼接条件", 入锁拼接条件, "补货区拼接条件",补货区拼接条件)
	
	$库位承载 = 查表("R101_基础数据_库位承载类型表",$库位定位分类.库位承载类型)
	$混放类型=查表("R101_库位混放类型规则表",$库位定位分类.混放类型)
	如果($可上架库位集 != 空 && 列表大小($可上架库位集) != 0) {
		每个($库位 : $可上架库位集.库位) {
			$库位.承载托数 = $库位承载.托数		    		
			$库位.承载重量 = $库位承载.重量
			$库位.承载体积 = $库位承载.体积
			$库位.承载数量 = $库位承载.数量
			
			$库位.已上架数量 = 0
			$库位.已放满 = "否"
			
			$库位.已上架货品包装重量 = 0
			$库位.已上架货品包装体积 = 0
			$库位.混放类型=$混放类型
			加入列表($可上架库位列表, $库位)
		}
	}
}

如果($可上架库位列表 != 空 && 列表大小($可上架库位列表) != 0) {
	列表对象排序($可上架库位列表, "动线号","升序", "库位占用比例","升序")
	每个($库位 : $可上架库位列表) {
		
		$自增标识 = 0
		
		如果($库位.库位占用比例  > 0){
	    	每个($库存 : $库位.库存){
	    		如果($库存.数量  > 0) {
	    			如果($库存.包装重量 > $库位.已上架货品包装重量){
		    			$库位.已上架货品包装重量 = $库存.包装重量
		    		}
		    		如果($库存.包装体积 > $库位.已上架货品包装体积){
		    			$库位.已上架货品包装体积 = $库存.包装体积
		    		}
	    		}
	    	}  
	    }
		
		每个($拣货分配结果 : 拣货分配返回列表) {
			如果($库位.已放满 == "否") {
			
				$混放是否成立=库位混放校验($库位, $库位.混放类型, $拣货分配结果.货品序号, $拣货分配结果.批次序号).混放是否成立
				如果($混放是否成立 == 1) {
					$包装单位 = 数据查询("R101_货品包装数据源", "货品序号",$拣货分配结果.货品序号, "包装级别", 补货区特性.包装级别)
					$待上架总数量 = $拣货分配结果.分配数量 / $包装单位.包装件装量
					$待上架总数量 = 舍尾取整($待上架总数量)
					
					如果($待上架总数量 > 0) {
						如果($库位.已上架货品包装重量 < $包装单位.包装重量){
		    				$库位.已上架货品包装重量 = $包装单位.包装重量
		    			}
						如果($库位.已上架货品包装体积 < $包装单位.包装体积) {
							$库位.已上架货品包装体积 = $包装单位.包装体积
						}
					
						如果($库位.承载重量 == 0){
							$库位.承载重量 = 10000
						} 
						如果($库位.承载体积 == 0){
							$库位.承载体积 = 10000
						}
						如果($库位.承载数量 == 0){
							$库位.承载数量 = 9000000
						}
						$按重量总可上架数量 = $库位.承载重量 / $库位.已上架货品包装重量
					    $按体积总可上架数量 = $库位.承载体积 / $库位.已上架货品包装体积
					    $按数量总可上架数量 = $库位.承载数量

					    // 打印("----按重量总可上架数量:" + $按重量总可上架数量)
						// 打印("----按体积总可上架数量:" + $按体积总可上架数量)
						// 打印("----按数量总可上架数量:" + $按数量总可上架数量)
					    
					    $按重量已上架数量 = $按重量总可上架数量 * $库位.库位占用比例 / 100
					    $按体积已上架数量 = $按体积总可上架数量 * $库位.库位占用比例 / 100      
					    $按数量已上架数量 = $按数量总可上架数量 * $库位.库位占用比例 / 100
					    
					    $按重量可上架数量 = $按重量总可上架数量 - $按重量已上架数量 - $库位.已上架数量
					    $按体积可上架数量 = $按体积总可上架数量 - $按体积已上架数量 - $库位.已上架数量 
					    $按数量可上架数量 = $按数量总可上架数量 - $按数量已上架数量 - $库位.已上架数量
					                
					    $按重量可上架数量 = 舍尾取整($按重量可上架数量)
					    $按体积可上架数量 = 舍尾取整($按体积可上架数量)
					    $按数量可上架数量 = 舍尾取整($按数量可上架数量)
					    
						$可上架数量 = 最小($按重量可上架数量, $按体积可上架数量, $按数量可上架数量)
						$可上架数量 = 舍尾取整($可上架数量)

						// 打印("----已上架货品包装重量:" + $库位.已上架货品包装重量)
						// 打印("----已上架货品包装体积:" + $库位.已上架货品包装体积)
						// 打印("----承载数量:" + $库位.承载数量)
						// 打印("----可上架数量:" + $可上架数量)
						// 打印("----待上架总数量:" + $待上架总数量)
						// 打印("----包装单位:" + $包装单位)
						
						如果($可上架数量 > 0) {
							如果($待上架总数量 > 0  && $可上架数量 >= $待上架总数量) {
								$临时对象 = 创建对象()
								$自增标识 = $自增标识 + 1
								$临时对象.自增标识 = $自增标识
								$临时对象.库存序号 = $拣货分配结果.库存序号
								$临时对象.库位序号 = $库位.库位序号
								$临时对象.库位代码 = $库位.库位代码
								$临时对象.上架数量 = $待上架总数量
								$临时对象.上架数量BU = $待上架总数量*$包装单位.包装件装量
								
								$临时对象.包装序号 = $包装单位.包装序号
								
								加入列表(上架分配返回列表, $临时对象)	
								
								$库位.已上架数量 = $库位.已上架数量 + $待上架总数量
								
								$库位.当前货品序号 = $拣货分配结果.货品序号
								$库位.当前批次序号 = $拣货分配结果.批次序号
								$待上架总数量 = 0
								$拣货分配结果.分配数量 = 0
							}
							如果($待上架总数量 > 0  && $可上架数量 > 0 && $可上架数量 < $待上架总数量){
								$临时对象 = 创建对象()
								$自增标识 = $自增标识 + 1
								$临时对象.自增标识 = $自增标识
								$临时对象.库存序号 = $拣货分配结果.库存序号
								$临时对象.库位序号 = $库位.库位序号
								$临时对象.库位代码 = $库位.库位代码
								$临时对象.上架数量 = $可上架数量
								$临时对象.上架数量BU = $可上架数量*$包装单位.包装件装量
								$临时对象.包装序号 = $包装单位.包装序号
								
								加入列表(上架分配返回列表, $临时对象)
								
								$库位.已上架数量 = $库位.已上架数量 + $可上架数量
								$库位.已放满 = "是"
								
								$库位.当前货品序号 = $拣货分配结果.货品序号
								$库位.当前批次序号 = $拣货分配结果.批次序号
								$待上架总数量 = $待上架总数量 - $可上架数量
								$拣货分配结果.分配数量 = $待上架总数量*$包装单位.包装件装量
							}
						}
					}
				}
			}
		}
	}
}