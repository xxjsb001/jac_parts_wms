码托方式 = 查表("R101_基础数据_码托规则表", 货主, 码托分类, 包装级别)

码托详情列表 = 创建列表()

如果(码托方式 == 空 ||单据类型 =="退货入库单") {
    $码托详情 = 创建对象()
	
    $码托详情.托盘号 = "-"
    $码托详情.数量 = 收货数量
    
    加入列表(码托详情列表, $码托详情)
}

如果(码托方式 != 空 && 单据类型 !="退货入库单") {
    $每托数量 = 码托方式.单层数量 * 码托方式.码放层数
    
    如果(收货数量 >= 码托方式.起码数量 && 收货状态 == "-") {
        $收货托数 = 舍尾取整(收货数量 / $每托数量)
	
        $整托 = 创建列表($收货托数)
    
        每个($循环序号 : $整托) {
            $码托详情 = 创建对象()
	        
            $码托详情.托盘号 = 生成托盘号().托盘号
            $码托详情.数量 = $每托数量
	        
            加入列表(码托详情列表, $码托详情)
        }
    
        $总整托数量 = $收货托数 * $每托数量
        $散托数量 = 收货数量 - $总整托数量
    
        如果($散托数量 > 0 && 码托方式.起码数量 <= $散托数量) {
            $码托详情 = 创建对象()
	        
            $码托详情.托盘号 = 生成托盘号().托盘号
            $码托详情.数量 = $散托数量
	        
            加入列表(码托详情列表, $码托详情)
        }
	    
        如果($散托数量 > 0 && 码托方式.起码数量 > $散托数量) {
            $码托详情 = 创建对象()
	    	
            $码托详情.托盘号 = "-"
            $码托详情.数量 = $散托数量
	        
            加入列表(码托详情列表, $码托详情)
        }
    }
	
    如果(收货数量 < 码托方式.起码数量 || 收货状态 != "-") {
        $码托详情 = 创建对象()
	    	
        $码托详情.托盘号 = "-"
        $码托详情.数量 = 收货数量
        
        加入列表(码托详情列表, $码托详情)
    }
}
